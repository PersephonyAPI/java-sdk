plugins {
	id 'net.saliman.cobertura' version '2.5.4'
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'com.github.PersephonyAPI'

sourceCompatibility = 1.7 // java 7
targetCompatibility = 1.7

repositories {
	mavenCentral()
}

dependencies {
	testCompile "org.mockito:mockito-core:2.6.8"
	testCompile 'org.mock-server:mockserver-netty:3.10.4'
	testCompile 'info.cukes:cucumber-java:1.2.4'
	testCompile 'info.cukes:cucumber-junit:1.2.4'
	testCompile 'junit:junit:4.12'

	compile 'com.google.code.gson:gson:2.6.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

test {
	testLogging.showStandardStreams = true
	systemProperties System.getProperties()
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

// To specify a license in the pom:
install {
	repositories.mavenInstaller {
		pom.project {
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
		}
	}
}

